apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
android {
    compileSdkVersion sdk.compileSdkVersion

    defaultConfig {
        minSdkVersion sdk.minSdkVersion
        targetSdkVersion sdk.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation "junit:junit:$test.junit"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'com.findsdk.library:fileprovider:1.1.1'

}

//--------------------
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

Properties properties = new Properties()
properties.load(project.file('bintray.properties').newDataInputStream())


//项目主页
def siteUrl = properties.getProperty("SITE_URL")
//项目的版本控制地址
def gitUrl = properties.getProperty("GIT_URL")

def repoName = properties.getProperty("REPO_NAME")
//发布到组织名称名字，必须填写
group = properties.getProperty("GROUP_ID")
//发布到JCenter上的项目名字，必须填写
def libName = properties.getProperty("ARTIFACT_ID")
// 版本号，下次更新是只需要更改版本号即可
version = properties.getProperty("LIB_VERSION_NAME")
/**  上面配置后上传至JCenter后的编译路径是这样的： compile 'group:libName:version'  **/
def packagType = properties.getProperty("PACKAG_TYPE")
def description = properties.getProperty("DESCRIPTION")

def DEVELOPER_ID = properties.getProperty("DEVELOPER_ID")
def DEVELOPER_NAME = properties.getProperty("DEVELOPER_NAME")
def DEVELOPER_EMAIL = properties.getProperty("DEVELOPER_EMAIL")

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    excludes = ['**/*.kt']
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    options.links siteUrl
    failOnError false
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 根节点添加
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging packagType
                name libName
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer { //填写的一些基本信息
                        id DEVELOPER_ID
                        name DEVELOPER_NAME
                        email DEVELOPER_EMAIL
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//上传到JCenter
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = repoName
        name = libName    //发布到JCenter上的项目名字，必须填写
        desc = description    //项目描述
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}