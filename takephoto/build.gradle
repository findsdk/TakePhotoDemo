apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
android {
    compileSdkVersion sdk.compileSdkVersion

    defaultConfig {
        minSdkVersion sdk.minSdkVersion
        targetSdkVersion sdk.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$test.espresso_core"
    testImplementation "junit:junit:$test.junit"
    implementation "com.android.support:appcompat-v7:$support.appcompat_v7"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    //Rxjava
    implementation "io.reactivex.rxjava2:rxandroid:$lib.rxandroid"
    implementation "io.reactivex.rxjava2:rxjava:$lib.rxjava"

    implementation "com.findsdk.library:fileprovider:1.0.0"
    implementation "com.findsdk.library:rxbus:1.0.0"
}

//--------------------
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//项目主页
def siteUrl = SITE_URL
//项目的版本控制地址
def gitUrl = GIT_URL

//发布到组织名称名字，必须填写
group = GROUP_ID
//发布到JCenter上的项目名字，必须填写
def libName = ARTIFACT_ID
// 版本号，下次更新是只需要更改版本号即可
version = LIB_VERSION_NAME
/**  上面配置后上传至JCenter后的编译路径是这样的： compile 'group:libName:version'  **/

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    excludes = ['**/*.kt']
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    options.links SITE_URL
    failOnError false
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}


// 根节点添加
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging PACKAG_TYPE
                name libName
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer { //填写的一些基本信息
                        id DEVELOPER_ID
                        name DEVELOPER_NAME
                        email DEVELOPER_EMAIL
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//上传到JCenter
Properties properties = new Properties()
properties.load(project.rootProject.file('gradle.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        repo = REPO_NAME
        name = libName    //发布到JCenter上的项目名字，必须填写
        desc = DESCRIPTION    //项目描述
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}